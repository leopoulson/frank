include std

include map

---------------- Helpers
snocOrInsert : {Sig -> X -> Map Sig (List X) -> Map Sig (List X)}
snocOrInsert sig x mnil = mentry sig [x] mnil
snocOrInsert sig x (mentry sig' xs rest) =
    if (eqSig sig sig')
      { mentry sig' (snoc xs x) rest }
      { mentry sig' xs (snocOrInsert sig x rest) }

-- equality between signals
eqSig : {Sig -> Sig -> Bool}
eqSig (opIntSig _) (opIntSig _) = true
eqSig (opStrSig _) (opStrSig _) = true
eqSig _ _ = false

peek : {Sig -> Map Sig (List X) -> Maybe X}
peek _ mnil = nothing
peek sig (mentry sig' xs rest) =
    if (eqSig sig sig')
       { case (xs)
         { nil -> nothing
         | (cb :: cbs) -> just cb }}
       { peek sig rest }

tail : {List X -> List X}
tail nil = nil
tail (x::xs) = xs

pop : {Sig -> Map Sig (List X) -> Map Sig (List X)}
pop _ mnil = mnil
pop sig (mentry sig' xs rest) =
    if (eqSig sig sig')
       { mentry sig' (tail xs) rest}
       { mentry sig' xs (pop sig rest) }
---------------- end helpers

-- Promise interface
interface Promise [E] =
          promise R : Prom R [E|] -> Pid R
        | signal : Sig -> Unit
        | await R : Pid R -> R

interface Tick = tick : Unit

data Prom R [E] = opIntCb {Int -> [E|] R} | opStrCb {String -> [E|] R}

-- TODO: Change this to be labels; so that it's op S Int?
data Sig = opIntSig Int | opStrSig String

data Pid X = pid (Ref (Maybe X))

readPid : {Pid X -> [RefState] (Maybe X)}
readPid (pid cell) = read cell

pr : {Prom Unit [Console]}
pr! = opIntCb {x -> print "running "; ouint (x + 10); print " thanks!\n"; unit}

client : {[Promise [Console], Console]Unit}
client! = print "starting!\n";
          let res = promise (opIntCb {x -> print "running "; ouint (x + 10); print " thanks!\n"; unit} ) in
          let resStr = promise (opStrCb {st -> print "stringing "; print st; print " thanks!\n"; unit} ) in
          await res;
          await resStr;
          print "Finished!\n";
          unit

server : {Int -> [Promise, Console, Tick] Unit}
server x = sleep 400000; tick!; signal (opIntSig x);
           sleep 400000; tick!; signal (opStrSig "hello!")

-- Given a pid and a callback, rewrites the callback such that it writes the
-- result to the cell and returns unit, whilst still performing effects as before.
-- Adds RefState to the ambient in the promise.
to_write : {Pid R -> {S -> [E|]R} -> {S -> [E| RefState] Unit}}
to_write (pid cell) cb = {x -> (write cell (just (<RefState>(cb x))))}

-- Converts a function of Int -> Unit to one of Sig -> unit.
prep_int : {{Int -> [E|]Unit} -> {Sig -> [E|]Unit}}
prep_int cb = {s -> case (s)
                    { (opIntSig x) -> cb x
                    | _ -> unit }}

-- as above, for strings.
prep_str : {{String -> [E|] Unit} -> {Sig -> [E|]Unit}}
prep_str cb = {st -> case (st)
                     { (opStrSig st) -> cb st
                     | _ -> unit}}

-- Handler / scheduler
hdl : {Map Sig (List {Sig -> [Console, RefState]Unit})                                      -- Promise storer
    -> Maybe (Pair (Pid R) {R -> [Console, Promise [Console], RefState] Unit})              -- possibly suspended client
    -> <Promise [Console]> Unit      -- client
    -> <Promise, Tick> Unit                    -- server
    -> [Console, RefState] Unit}

-- Installing promises
-- cbMod is the original callback, modified to make its input and return types generic.
hdl cbs res <promise (opIntCb cb) -> client> <server> =
    let cell = pid (new nothing) in
    -- look at how they massacred my boy!
    let cbMod = prep_int (<Console> (to_write cell cb)) in
    hdl (snocOrInsert (opIntSig 0) cbMod cbs) res (client cell) server!

-- same for strings
hdl cbs res <promise (opStrCb cb) -> client> <server> =
    let cell = pid (new nothing) in
    let cbMod = prep_str (<Console> (to_write cell cb)) in
    hdl (snocOrInsert (opStrSig "") cbMod cbs) res (client cell) server!

-- Processing incoming signals without blocked computation.
hdl cbs nothing <client> <signal sig -> server> =
    -- Look to see if there's any promises of this type waiting.
    case (peek sig cbs)
      -- if not, do nothing and return.
      { nothing -> hdl cbs nothing client! (server unit)
      -- if there is, run it on the client's side and remove it.
      | (just cb) -> hdl (pop sig cbs) nothing (<Promise> (cb sig); client!) (server unit)}

-- Processing incoming signals with blocked comp.
hdl cbs (just (pair cell resumption)) unit <signal sig -> server> =
    -- If there's no callbacks of this type to run, just don't do anything.
    case (peek sig cbs)
     { nothing -> hdl cbs (just (pair cell resumption)) unit (server unit)
     | (just cb) ->
       -- First run the callback.
       let cbResult = cb sig in
       -- Then remove it from storage.
       let poppedCbs = pop sig cbs in
       -- Then check if the cell has had a value put in it as a result.
       case (readPid cell)
          -- if we have a result, apply whatever the result of the comp is and then resume
          { (just result) -> hdl poppedCbs nothing (cbResult; resumption result) (server unit)
          -- if we do not, just try again
          | nothing -> hdl poppedCbs (just (pair cell resumption)) unit (server unit)}}

hdl cbs res <await cell -> client> <server> =
    case (readPid cell)
      { (just x) -> hdl cbs nothing (client x) server!
      | nothing  -> hdl cbs (just (pair cell client)) unit server! }

hdl cbs res <client> <tick -> server> =
    hdl cbs res client! (server unit)

hdl _ _ unit _ = unit
hdl _ _ _ unit = unit

main : {[Console, RefState] Unit}
main! = hdl mnil nothing (<RefState>(client!)) (<RefState>(server 10))
